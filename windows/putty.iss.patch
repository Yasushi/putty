--- putty.iss.orig	2009-03-06 12:07:13.157208649 +0900
+++ putty.iss	2009-04-07 17:35:42.114604696 +0900
@@ -13,22 +13,36 @@
 ;    UninstallDisplayIcon, though.
 
 [Setup]
-AppName=PuTTY
-AppVerName=PuTTY version 0.60
-VersionInfoTextVersion=Release 0.60
+AppName={cm:MyAppName}
+AppVerName={cm:MyAppVerName}
+VersionInfoTextVersion={cm:MyVersionInfo}
 AppVersion=0.60
 VersionInfoVersion=0.60.0.0
-AppPublisher=Simon Tatham
-AppPublisherURL=http://www.chiark.greenend.org.uk/~sgtatham/putty/
-AppReadmeFile={app}\README.txt
-DefaultDirName={pf}\PuTTY
-DefaultGroupName=PuTTY
+AppPublisher=Yebisuya Hompo
+AppPublisherURL=http://yebisuya.dip.jp/Software/PuTTY/
+AppReadmeFile={app}\readme-putty-0.59-JP_Y.html
+DefaultDirName={code:DefaultBaseDir}\PuTTY
+DefaultGroupName={cm:MyAppName}
 SetupIconFile=puttyins.ico
 UninstallDisplayIcon={app}\putty.exe
 ChangesAssociations=yes
 ;ChangesEnvironment=yes -- when PATH munging is sorted (probably)
 Compression=zip/9
 AllowNoIcons=yes
+AppId=PuTTY
+PrivilegesRequired=none
+OutputBaseFilename=putty-0.60-JP_Y-setup
+
+[Languages]
+Name: "eng"; MessagesFile: "compiler:Default.isl,Default.isl"
+Name: "jpn"; MessagesFile: "compiler:Languages\Japanese.isl,Japanese.isl"
+
+[Components]
+Name: PuTTY; Description: "{cm:MyAppName}({cm:PuTTYComment})"; Types: Full Custom Compact; Flags: fixed
+Name: pfwd; Description: "{cm:PfwdComponentDescription}"; Types: Full
+Name: plinkw; Description: "{cm:PlinkwComponentDescription}"; Types: Full
+Name: jpnlng; Description: "{cm:JpnLngComponentDescription}"; Types: Full; Languages: jpn
+Name: source; Description: "{cm:SourceComponentDescription}"; Types: Full
 
 [Files]
 ; We flag all files with "restartreplace" et al primarily for the benefit
@@ -54,53 +68,73 @@
 Source: "..\doc\putty.hlp"; DestDir: "{app}"; Flags: restartreplace uninsrestartdelete
 Source: "..\doc\putty.cnt"; DestDir: "{app}"; Flags: restartreplace uninsrestartdelete
 Source: "..\LICENCE"; DestDir: "{app}"; Flags: restartreplace uninsrestartdelete
-Source: "README.txt"; DestDir: "{app}"; Flags: isreadme restartreplace uninsrestartdelete
+Source: "README.txt"; DestDir: "{app}"; Flags: restartreplace uninsrestartdelete
+Source: "plinkw.exe"; DestDir: "{app}"; Components: plinkw
+Source: "pfwd.exe"; DestDir: "{app}"; Components: pfwd
+Source: "pfwd_sample.ini"; DestDir: "{app}"; Components: pfwd
+Source: "pageant.lng"; DestDir: "{app}"; Components: jpnlng
+Source: "pfwd.lng"; DestDir: "{app}"; Components: jpnlng and pfwd
+Source: "plinkw.lng"; DestDir: "{app}"; Components: jpnlng and plinkw
+Source: "putty.lng"; DestDir: "{app}"; Components: jpnlng
+Source: "puttygen.lng"; DestDir: "{app}"; Components: jpnlng
+Source: "puttytel.lng"; DestDir: "{app}"; Components: jpnlng
+Source: "..\..\putty-doc\readme-putty-0.60-JP_Y.html"; DestDir: "{app}"; Flags: isreadme
+Source: "..\..\putty-doc\putty-0.60-JP_Y.patch"; DestDir: "{app}"; Components: source
+Source: "puttyd.ico"; DestDir: "{app}"; Components: source
 
 [Icons]
-Name: "{group}\PuTTY"; Filename: "{app}\putty.exe"
+Name: "{group}\{cm:PuTTYShortcutName}"; Filename: "{app}\putty.exe"
 ; We have to fall back from the .chm to the older .hlp file on some Windows
 ; versions.
-Name: "{group}\PuTTY Manual"; Filename: "{app}\putty.chm"; MinVersion: 4.1,5.0
-Name: "{group}\PuTTY Manual"; Filename: "{app}\putty.hlp"; OnlyBelowVersion: 4.1,5.0
-Name: "{group}\PuTTY Web Site"; Filename: "{app}\website.url"
-Name: "{group}\PSFTP"; Filename: "{app}\psftp.exe"
-Name: "{group}\PuTTYgen"; Filename: "{app}\puttygen.exe"
-Name: "{group}\Pageant"; Filename: "{app}\pageant.exe"
-Name: "{commondesktop}\PuTTY"; Filename: "{app}\putty.exe"; Tasks: desktopicon\common
-Name: "{userdesktop}\PuTTY"; Filename: "{app}\putty.exe"; Tasks: desktopicon\user
+Name: "{group}\{cm:PuTTYManualShortcutName}"; Filename: "{app}\putty.chm"; MinVersion: 4.1,5.0
+Name: "{group}\{cm:PuTTYManualShortcutName}"; Filename: "{app}\putty.hlp"; OnlyBelowVersion: 4.1,5.0
+Name: "{group}\{cm:PuTTYWebSiteShortcutName}"; Filename: "{app}\website.url"
+Name: "{group}\{cm:PSFTPShortcutName}"; Filename: "{app}\psftp.exe"
+Name: "{group}\{cm:PuTTYgenShortcutName}"; Filename: "{app}\puttygen.exe"
+Name: "{group}\{cm:PageantShortcutName}"; Filename: "{app}\pageant.exe"
+Name: "{commondesktop}\{cm:PuTTYShortcutName}"; Filename: "{app}\putty.exe"; Tasks: desktopicon and common
+Name: "{userdesktop}\{cm:PuTTYShortcutName}"; Filename: "{app}\putty.exe"; Tasks: desktopicon and not common
 ; Putting this in {commonappdata} doesn't seem to work, on 98SE at least.
-Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\PuTTY"; Filename: "{app}\putty.exe"; Tasks: quicklaunchicon
+Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{cm:PuTTYShortcutName}"; Filename: "{app}\putty.exe"; Tasks: quicklaunchicon
 
 [Tasks]
-Name: desktopicon; Description: "Create a &desktop icon for PuTTY"; GroupDescription: "Additional icons:"; Flags: unchecked
-Name: desktopicon\common; Description: "For all users"; GroupDescription: "Additional icons:"; Flags: exclusive unchecked
-Name: desktopicon\user; Description: "For the current user only"; GroupDescription: "Additional icons:"; Flags: exclusive unchecked
-Name: quicklaunchicon; Description: "Create a &Quick Launch icon for PuTTY (current user only)"; GroupDescription: "Additional icons:"; Flags: unchecked
-Name: associate; Description: "&Associate .PPK files (PuTTY Private Key) with Pageant and PuTTYgen"; GroupDescription: "Other tasks:"
+Name: desktopicon; Description: "{cm:desktopiconDescription}"; GroupDescription: "{cm:AdditionalIconsGroupDescription}"; Flags: unchecked
+Name: quicklaunchicon; Description: "{cm:quicklaunchiconDescription}"; GroupDescription: "{cm:AdditionalIconsGroupDescription}"; Flags: unchecked
+Name: associate; Description: "{cm:associateDescription}"; GroupDescription: "{cm:OtherTasksGroupDescription}"
+Name: saveini; Description: "{cm:SaveiniDescription}"; GroupDescription: "{cm:OtherTasksGroupDescription}"; Flags: unchecked
+Name: common; Description: "{cm:desktopicon_commonDescription}"; GroupDescription: "{cm:OtherTasksGroupDescription}"; Flags: unchecked
 
 [Registry]
-Root: HKCR; Subkey: ".ppk"; ValueType: string; ValueName: ""; ValueData: "PuTTYPrivateKey"; Flags: uninsdeletevalue; Tasks: associate
-Root: HKCR; Subkey: "PuTTYPrivateKey"; ValueType: string; ValueName: ""; ValueData: "PuTTY Private Key File"; Flags: uninsdeletekey; Tasks: associate
-Root: HKCR; Subkey: "PuTTYPrivateKey\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\pageant.exe,0"; Tasks: associate
-Root: HKCR; Subkey: "PuTTYPrivateKey\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\pageant.exe"" ""%1"""; Tasks: associate
-Root: HKCR; Subkey: "PuTTYPrivateKey\shell\edit"; ValueType: string; ValueName: ""; ValueData: "&Edit"; Tasks: associate
-Root: HKCR; Subkey: "PuTTYPrivateKey\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\puttygen.exe"" ""%1"""; Tasks: associate
+Root: HKCR; Subkey: ".ppk"; ValueType: string; ValueName: ""; ValueData: "PuTTYPrivateKey"; Flags: uninsdeletevalue; Tasks: associate and common
+Root: HKCR; Subkey: "PuTTYPrivateKey"; ValueType: string; ValueName: ""; ValueData: "{cm:PPKTypeDescription}"; Flags: uninsdeletekey; Tasks: associate and common
+Root: HKCR; Subkey: "PuTTYPrivateKey\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\pageant.exe,0"; Tasks: associate and common
+Root: HKCR; Subkey: "PuTTYPrivateKey\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\pageant.exe"" ""%1"""; Tasks: associate and common
+Root: HKCR; Subkey: "PuTTYPrivateKey\shell\edit"; ValueType: string; ValueName: ""; ValueData: "{cm:PPKEditLabel}"; Tasks: associate and common
+Root: HKCR; Subkey: "PuTTYPrivateKey\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\puttygen.exe"" ""%1"""; Tasks: associate and common
+Root: HKCU; Subkey: "Software\Classes\.ppk"; ValueType: string; ValueName: ""; ValueData: "PuTTYPrivateKey"; Flags: uninsdeletevalue; Tasks: associate and not common
+Root: HKCU; Subkey: "Software\Classes\PuTTYPrivateKey"; ValueType: string; ValueName: ""; ValueData: "{cm:PPKTypeDescription}"; Flags: uninsdeletekey; Tasks: associate and not common
+Root: HKCU; Subkey: "Software\Classes\PuTTYPrivateKey\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\pageant.exe,0"; Tasks: associate and not common
+Root: HKCU; Subkey: "Software\Classes\PuTTYPrivateKey\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\pageant.exe"" ""%1"""; Tasks: associate and not common
+Root: HKCU; Subkey: "Software\Classes\PuTTYPrivateKey\shell\edit"; ValueType: string; ValueName: ""; ValueData: "{cm:PPKEditLabel}"; Tasks: associate and not common
+Root: HKCU; Subkey: "Software\Classes\PuTTYPrivateKey\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\puttygen.exe"" ""%1"""; Tasks: associate and not common
 ; Add to PATH on NT-class OS?
 
+[INI]
+Filename: "{app}\putty.ini"; Section: "Generic"; Key: "UseIniFile"; Tasks: saveini and common
+Filename: "{userappdata}\PuTTY\putty.ini"; Section: "Generic"; Key: "UseIniFile"; Tasks: saveini and not common
+
 [UninstallRun]
 ; -cleanup-during-uninstall is an undocumented option that tailors the
 ; message displayed.
 ; XXX: it would be nice if this task weren't run if a silent uninstall is
 ;      requested, but "skipifsilent" is disallowed.
-Filename: "{app}\putty.exe"; Parameters: "-cleanup-during-uninstall"; RunOnceId: "PuTTYCleanup"; StatusMsg: "Cleaning up saved sessions etc (optional)..."
+Filename: "{app}\putty.exe"; Parameters: "-cleanup-during-uninstall"; RunOnceId: "PuTTYCleanup"; StatusMsg: "{cm:UninstallStatusMsg}"
 
-[Messages]
-; Since it's possible for the user to be asked to restart their computer,
-; we should override the default messages to explain exactly why, so they
-; can make an informed decision. (Especially as 95% of users won't need or
-; want to restart; see rant above.)
-FinishedRestartLabel=One or more [name] programs are still running. Setup will not replace these program files until you restart your computer. Would you like to restart now?
-; This message is popped up in a message box on a /SILENT install.
-FinishedRestartMessage=One or more [name] programs are still running.%nSetup will not replace these program files until you restart your computer.%n%nWould you like to restart now?
-; ...and this comes up if you try to uninstall.
-UninstalledAndNeedsRestart=One or more %1 programs are still running.%nThe program files will not be removed until your computer is restarted.%n%nWould you like to restart now?
+[Code]
+function DefaultBaseDir(Param: String) : String;
+begin
+    if IsAdminLoggedOn then
+      Result := ExpandConstant('{pf}')
+    else
+      Result := ExpandConstant('{userappdata}');
+end;
